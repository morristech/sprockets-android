apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'me.tatarka.retrolambda'

def libName = 'Sprockets Android'
def libVersionCode = 4_000_000
def libVersionName = '4.0.0'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.3'
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 25
        versionCode libVersionCode
        versionName libVersionName
        consumerProguardFiles 'proguard-rules.pro'
        multiDexEnabled true
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }
    dataBinding {
        enabled true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/INDEX.LIST' // objenesis, commons-primitives
    }
}

dependencies {
    def supportLibraryVersion = '25.3.1'
    def playServicesVersion = '10.2.6'

    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile "com.android.support:design:$supportLibraryVersion"
    compile "com.android.support:support-v13:$supportLibraryVersion"
    provided 'com.firebaseui:firebase-ui-auth:1.2.0' // change to compile when fabric repo gone
    compile "com.google.android.gms:play-services-location:$playServicesVersion"
    compile "com.google.android.gms:play-services-places:$playServicesVersion"
    compile 'com.google.dagger:dagger-android:2.11'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.11'
    provided 'com.google.errorprone:error_prone_annotations:2.0.19'
    compile "com.google.firebase:firebase-auth:$playServicesVersion"
    compile "com.google.firebase:firebase-config:$playServicesVersion"
    compile "com.google.firebase:firebase-crash:$playServicesVersion"
    compile "com.google.firebase:firebase-perf:$playServicesVersion"
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'io.reactivex.rxjava2:rxjava:2.1.0'
    compile 'io.realm:android-adapters:2.1.0'
    provided 'io.realm:realm-android-library:3.3.1' // don't force big jni libs on client
    compile 'jp.wasabeef:picasso-transformations:2.1.2'
    compile 'net.sf.sprockets:sprockets:4.0.0'
    compile 'org.apache.commons:commons-collections4:4.1'
    provided 'org.immutables:value:2.5.2'
    compile 'pub.devrel:easypermissions:0.4.0'
    androidTestCompile project(':sprockets-test')
    androidTestCompile 'com.firebaseui:firebase-ui-auth:1.2.0' // only provided above (for now)
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task androidJavadocs(type: Javadoc, dependsOn: 'compileReleaseJavaWithJavac') {
    def apt = "${buildDir}/generated/source/apt/release/net/sf/sprockets"
    source = android.sourceSets.main.java.srcDirs
            .plus(fileTree(dir: apt, include: ['**/Mutable*.java']))
    classpath += files(android.bootClasspath, configurations.compile, configurations.provided,
            fileTree(dir: "${android.sdkDirectory}/extras", include: ['**/classes.jar']))
    title = "${libName} ${libVersionName} API"
    options {
        links 'http://reactivex.io/RxJava/2.x/javadoc/', 'https://developer.android.com/reference/',
                'https://docs.oracle.com/javaee/7/api/', 'https://realm.io/docs/java/latest/api/',
                'https://square.github.io/picasso/2.x/picasso/'
        linksOffline 'https://developers.google.com/android/reference/',
                "${android.sdkDirectory}/extras/google/google_play_services/docs/reference"
    }
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

artifacts {
    archives androidSourcesJar, androidJavadocsJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file://${System.properties['user.home']}/.m2/repository/")
            pom.project {
                groupId 'net.sf.sprockets'
                artifactId 'sprockets-android'
                version libVersionName
                packaging 'aar'
                name libName
                description 'Android base components, widgets, and utility methods'
                url 'https://pushbit.github.io/sprockets-android'
                licenses {
                    license {
                        name 'GNU Lesser General Public License, Version 3'
                        url 'http://www.gnu.org/licenses/lgpl.txt'
                        distribution 'repo'
                    }
                }
                developers {
                    developer {
                        id 'pushbit'
                        name 'Paul Bitson'
                        email 'pushbit@gmail.com'
                    }
                }
                issueManagement {
                    system 'GitHub'
                    url 'https://github.com/pushbit/sprockets-android/issues'
                }
                scm {
                    connection 'scm:git:git://github.com/pushbit/sprockets-android.git'
                    url 'https://github.com/pushbit/sprockets-android'
                }
            }
        }
    }
}
